= GPars - Groovy Parallel Systems
The Whole GPars Team <gpars-developers@googlegroups.com>
v2.0, 2017-02-01
:linkattrs:
:linkcss:
:toc: right
:toc-title: Document Index
:icons: font
:source-highlighter: coderay
:docslink: http://gpars.org/[GPars Documentation]
:description: GPars is a multi-paradigm concurrency framework offering several mutually cooperating high-level concurrency abstractions.
:doctitle: Groovy Fast Track

Using this *Groovy Fast Track*, you will start experimenting with GPars in about 3 minutes. We assume you have Groovy installed on your system.

== Step 1 -- Start up the Groovy Console

Start a fresh Groovy Console or open up an empty groovy script source in your favorite IDE

== Step 2 -- Add dependencies

TIP: *GPars* comes bundled with Groovy distributions after version 1.8, so this step should normally be not required._

''''

We'll use Groovy's _Grape_ functionaity to grab all the requited dependencies for us. You may check out the link:Integration.html[*GPars* Integration page] for other ways to integrate *GPars* with your project.

Add the following line to the groovy script:

.Use the Grape Grab Feature
[source,groovy,linenums]
----
    @Grab(group='org.codehaus.gpars', module='gpars', version='2.0')
----

== Step 3 -- Experiment with parallel collection processing

Believe it or not, now, we're ready to experiment. Try the following script, which will concurrently query a collection of strings with regular expressions:

.Groovy Concurrent Sample
[source,groovy,linenums]
----
@Grab(group='org.codehaus.gpars', module='gpars', version='2.0')
import groovyx.gpars.GParsPool

GParsPool.withPool {
    def animals = ['dog', 'ant', 'cat', 'whale']
    println(animals.anyParallel {it ==~ /ant/} ? 'Found an ant' : 'No ants found')
    println(animals.everyParallel {it.contains('a')} ? 'All animals contain a' : 'Some animals can live without an a')
    }
----

Run the script and you should get the following output:

 . Found an ant
 . Some animals can live without an a

Now feel free to experiment by changing the regular expressions, using different collections or different methods, like _eachParallel()_, _collectParallel()_, _maxParallel()_, _sumParallel()_ and others. You get the idea, right?

To find out more about parallel collection processing, visit the Parallel Collections section of the link:./guide/[User Guide].

== Step 4 -- Actors

Now we could try to build an actor and send it a couple of messages to see it acting.

.An Actor Sample
[source,groovy,linenums]
----
@Grab(group='org.codehaus.gpars', module='gpars', version='1.2.0')
import groovyx.gpars.actor.DynamicDispatchActor
import org.codehaus.groovy.runtime.NullObject

final class MyActor extends DynamicDispatchActor {
    private int counter = 0

    void onMessage(String message) {
        counter += message.size()
        println 'Received string'
    }

    void onMessage(Integer message) {
        counter += message
        println 'Received integer'
    }

    void onMessage(Object message) {
        counter += 1
        println 'Received object'
    }

    void onMessage(NullObject message) {
        println 'Received a null object. Sending back the current counter value.'
        reply counter
    }
}

final def actor = new MyActor()
actor.start()
actor.send 1
actor << 2
actor 20
actor 'Hello'
println actor.sendAndWait(null)
----

Our actor maintains a private counter and accepts different types of messages, which result in updating the counter. 
Sending a null value will make the actor reply the current counter value back to us. 
Notice the _send()_ method name is optional and can be replaced by the _<<_ operator or ommited altogether.

The link:./guide/#_user_guide_to_actors[Actors review] of the link:./guide/[UserGuide] will help you dive deeper into *GPars* actors.

== Further Steps

Now when you have *GPars* runing on your system, now's the time to open up the link:./guide/[User Guide], browse the *GPars* code examples and continue experimenting. 

You may also consider checking out the link:Java_Fast_Track.html[Java Fast Track], in case you need to use *GPars* high-level concurency abstractions from Java code. Good luck!
